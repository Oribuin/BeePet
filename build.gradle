plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0" apply false
}

allprojects {

    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'xyz.oribuin'
    version = '1.0-SNAPSHOT'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url = 'https://libraries.minecraft.net' }
        maven { url = "https://repo.rosewooddev.io/repository/public/" }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url = 'https://jitpack.io/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    dependencies {
        compileOnly 'com.mojang:authlib:1.5.21' // Required for Mojang API
    }

}

dependencies {
    implementation project(path: ':Plugin')
    implementation project(path: ':NMS:Wrapper')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R3', configuration: 'remapped')
}

shadowJar {
    archiveClassifier.set(null)

    relocate("dev.rosewood.rosegarden", "${project.group}.beepet.libs.rosegarden")

    exclude 'dev/rosewood/rosegarden/lib/hikaricp/**/*.class'
    exclude 'dev/rosewood/rosegarden/lib/slf4j/**/*.class'
}

build.dependsOn shadowJar

